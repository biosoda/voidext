##########################
############# instances of voidext:ComplexLinkSet class composed of two voidext:SharedInstanceSet(s)
############# Question (3):
############# which are the set of virtual links between two similar or equivalent instances of unlike types in different RDF datasets? In other words, complex link sets composed of two Shared instance sets
##########################

prefix void:    <http://rdfs.org/ns/void#>
prefix bioquery: <http://purl.org/query/bioquery#>
prefix voidext:<http://purl.org/query/voidext#>
prefix dcterms: <http://purl.org/dc/terms/>

SELECT distinct ?links ?source_dataset1_name ?target_dataset2_name ?sharedType
  ?resourceMapping ?source_endpoint ?target_endpoint{
  
 #values(?source_db){ (bioquery:EBI) }
 #values(?target_db){ (bioquery:UNIPROT) }

 ?links a voidext:ComplexLinkSet.
 ?links voidext:intersectAt ?set1; voidext:intersectAt ?set2.
 ?set1  voidext:isSubsetOf ?target_db.
 ?target_db dcterms:title ?target_dataset2_name;
           void:sparqlEndpoint ?target_endpoint.
 ?set2 voidext:sharedInstanceType  ?sharedType .
 ?set2 voidext:isSubsetOf ?source_db .
 ?source_db dcterms:title ?source_dataset1_name;
            void:sparqlEndpoint ?source_endpoint.

 optional{   ?links voidext:recommendedMapping ?set2.
             ?set2 voidext:resourceMapping  ?resourceMapping}
 filter(?target_db != ?source_db)        
                         
} order by ?links
